/*
 * مثال لتطبيق دردشة معتمد على خاصية الويب سوكت في مكتبة Http.
 *
 * حقوق النشر © 2026 سرمد عبد الله
 *
 * هذه المكتبة برمجية حرة؛ يمكنك إعادة توزيعها و/أو تعديلها بموجب شروط
 * رخصة غنو العمومية الصغرى (LGPL) كما نشرتها مؤسسة البرمجيات الحرة؛ إما الإصدار
 * 3 من الرخصة، أو (حسب اختيارك) أي إصدار لاحق.
 *
 * توزع هذه المكتبة على أمل أن تكون مفيدة، ولكن دون أي ضمان؛ دون حتى
 * الضمان الضمني لقابلية التسويق أو الملاءمة لغرض معين. راجع رخصة غنو
 * العمومية الصغرى لمزيد من التفاصيل.
 *
 * من المفترض أن تكون قد تلقيت نسخة من رخصة غنو العمومية الصغرى مع
 * هذه المكتبة؛ وإلا فاحصل عليها من <https://www.gnu.org/licenses/>.
 */

اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛

وحدة غـرفة_الدردشة {
    استخدم مـتم؛

    صنف عـميل {
        عرف اتصال: مؤشر[بـننف.اتـصال]؛
    }

    عرف العملاء: مصفوفة[عـميل، 32]؛
    عرف عدد_العملاء: صـحيح = 0؛

    دالة عند_اتصال_ويب_سوكت(اتصال: مؤشر[بـننف.اتـصال]، _: مؤشر[فـراغ]): صـحيح {
        العملاء(عدد_العملاء).اتصال = اتصال؛
        عدد_العملاء += 1؛

        طـرفية.اطبع("عـميل اتصل\ج")؛
        أرجع 0؛
    }

    دالة عند_استعداد_ويب_سوكت(اتصال: مؤشر[بـننف.اتـصال]، _: مؤشر[فـراغ]): فـراغ {
        بـننف.اكتب_نصا_في_ويب_سوكت(
            اتصال،
            "اتصلت بغرفة الدردشة"
        )؛
    }

    دالة عند_بيانات_ويب_سوكت(
        اتصال: مؤشر[بـننف.اتـصال]،
        رمز_العملية: صـحيح،
        بيانات: مـؤشر_محارف،
        طول_البيانات: طـبيعي_متكيف،
        _: مؤشر[فـراغ]
    ): صـحيح {
        إذا (رمز_العملية & 1) == 0 أرجع 1؛

        عرف ت: صـحيح = 0؛
        بينما ت < عدد_العملاء و العملاء(ت).اتصال != اتصال {
            ++ت؛
        }
        عرف رسالة: نـص = نـص.املأ("مستخدم %i: %s"، ت + 1، نـص(بيانات، طول_البيانات).صوان)؛
        لكل ت = 0، ت < عدد_العملاء، ++ت {
            بـننف.اكتب_نصا_في_ويب_سوكت(
                العملاء(ت).اتصال،
                رسالة،
                رسالة.هات_الطول()
            )؛
        }

        أرجع 1؛
    }

    دالة عند_إغلاق_ويب_سوكت(اتصال: مؤشر[بـننف.اتـصال]، _: مؤشر[فـراغ]): فـراغ {
        عرف ت: صـحيح = 0؛
        بينما ت < عدد_العملاء {
            إذا العملاء(ت).اتصال == اتصال {
                عرف ج: صـحيح = ت؛
                بينما ج < عدد_العملاء - 1 {
                    العملاء(ج).اتصال = العملاء(ج + 1).اتصال؛
                    ++ج؛
                }

                --عدد_العملاء؛
                اقطع؛
            }
            ت += 1؛
        }

        طـرفية.اطبع("عميل انقطع\ج")؛
    }

    دالة معالج_طلب_بننف(اتصال: مؤشر[بـننف.اتـصال]): صـحيح {
        عرف معلومات_الطلب: مؤشر[بـننف.مـعلومات_الطلب] = بـننف.هات_معلومات_الطلب(اتصال)؛

        إذا نـص.أمتطابق(معلومات_الطلب~محتوى.المعرف_المحلي، "/websocket") أرجع 0؛

        إذا نـص.أمتطابق(معلومات_الطلب~محتوى.المعرف_المحلي، "/") {
            عرف محتوى_الصفحة: مـؤشر_محارف =
                "<!DOCTYPE html><html><body dir=rtl>"
                "<h2>غرفة الدردشة</h2>"
                "<input id='msg' placeholder='الرسالة'>"
                "<button onclick='أرسل()'>إرسال</button>"
                "<div id='صندوق'></div>"
                "<script>"
                "const ws=new WebSocket('ws://'+location.host+'/websocket');"
                "function أرسل(){"
                "ws.send(msg.value);"
                "msg.value='';"
                "}"
                "ws.onmessage=e=>{"
                "let d=document.createElement('div');"
                "d.textContent=e.data;"
                "صندوق.appendChild(d);};"
                "</script></body></html>"؛

            بـننف.اطبع(اتصال، "HTTP/1.1 200 OK\r\n")؛
            بـننف.اطبع(اتصال، "Content-Type: text/html; charset=utf-8\r\n")؛
            بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، نـص.هات_الطول(محتوى_الصفحة))؛
            بـننف.اطبع(اتصال، محتوى_الصفحة)؛
            أرجع 1؛
        }

        أرجع 1؛
    }

    دالة ابدأ() {
        عرف سياق: مؤشر[بـننف.سـياق] = بـننف.شغل_الخادم(معالج_طلب_بننف~مؤشر، "8080")؛

        بـننف.حدد_معالج_ويب_سوكت(
            سياق،
            "/websocket"،
            عند_اتصال_ويب_سوكت~مؤشر،
            عند_استعداد_ويب_سوكت~مؤشر،
            عند_بيانات_ويب_سوكت~مؤشر،
            عند_إغلاق_ويب_سوكت~مؤشر
        )؛

        طـرفية.اطبع("الخادم يعمل على المنفذ 8080\ج")؛
        طـرفية.أدخل_محرفا()؛
        بـننف.أوقف_الخادم(سياق)؛
    }
}

غـرفة_الدردشة.ابدأ()؛
