/*
 * Simple HTTP server example
 *
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

import "Srl/Console.alusus";
import "Srl/String.alusus";
import "Srl/Memory.alusus";
import "Apm.alusus";
Apm.importFile("Alusus/Http");

module TestModule {
    use Srl;
    func start() {
        def context: ptr[Http.Context] = Http.startServer(callbackRequest~ptr, "This is some user data!", "8080");
        Console.print("Server is listening on port 8080\nhttp://localhost:8080/\nPress enter to close server.");
        Console.getChar(); // stop proccess here waiting user to press enter
        Http.stopServer(context); // stop the server
        Console.print("Server closed.\nPress enter to exit.");
        Console.getChar(); // stop proccess here waiting user to press enter
    };

    func callbackRequest(connection: ptr[Http.Connection]): Int {
        def requestInfo: ptr[Http.RequestInfo] = Http.getRequestInfo(connection);
        def content: array[Char, 1024];

        String.assign(
            content~ptr,
            "<h1>Welcome from Alusus</h1><p> you are in \"%s\"</p><p>%s</p>",
            requestInfo~cnt.localUri,
            requestInfo~cnt.userData
        );
        Http.print(connection, "HTTP/1.1 200 OK\r\n");
        Http.print(connection, "Content-Type: text/html\r\n");
        Http.print(connection, "Content-Length: %d\r\n\r\n", String.getLength(content~ptr));
        Http.print(connection, content~ptr);

        return 1;
    };
};

TestModule.start();
